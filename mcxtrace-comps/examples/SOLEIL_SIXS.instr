/*******************************************************************************
* Instrument: SOLEIL_SIXS
*
* %Identification
* Written by: E. Farhi
* Date: 2023
* Origin: SOLEIL
* Version: 0.1
* %INSTRUMENT_SITE: SOLEIL
*
* A simple model for the SIXS beam-line at SOLEIL (surface diffraction).
*
* %Description
*
* SixS (Surface Interface X-ray Scattering) is a beamline dedicated to the study
* of X-ray scattering from surfaces and interfaces of hard and soft matter in
* various environments in the 5-20 keV energy range. To be sensitive to the
* surface all the studies are performed in grazing-incidence geometry. The
* beamline is equipped with two experimental hutches, which ae dedicated to the
* study of surfaces, interfaces and nano-objets prepared:
*
* - in-situ under UHV (Ultra High Vacuum, i.e. 10-10 mbar) conditions. A 
*   diffractometer allows to measure X-ray scattering from samples under UHV;
* - in various environments (catalysis chambers, soft matter, electrochemical
*   cells). A diffractometer coupled with exchangeable chambers will be able to
*   measure X-ray scattering from sample surfaces both in vertical or horizontal
*   geometries.
*
* Grazing Incidence X-ray Diffraction (GIXD), Grazing Incidence Small Angle
* X-ray Scattering (GISAXS), anomalous surface X-ray scattering, X-Ray
* Reflectivity (XRR), magnetic surface X-ray scattering and coherent scattering
* experiments are performed on both the facilities.
*
* In this implementation, the multipurpose diffractometer is used, with a thin
* single crystal layer on top of a Si bulk.
*
* Position | Element
* ---------|--------------------------------------------------------------------
* 0        | the U20 undulator
* 15       | Slit S1 2x0.7 mm^2
* 16.5     | a Si(111) DCM, 40x40x10 mm^3 E0=5-20 keV
* 26       | Mirror M1 Si coated with Pd, elliptically vertical focusing 20x350x20 mm^3 Incident angle 0.175 deg (3 mrad)
* 28       | Mirror M2 at 20x550x20mm^3. Can be tilted 45 deg exchange horz/vert components
* 33       | multipurpose diffractometer
* 42       | UHV diffractometer
*
* Example: E0=10 Detector: mon_spl_fluo_I=3.66859e+14
*
* %Parameters
* E0: [keV] Nominal energy at the Wiggler.
* dE: [keV] Energy half-bandwidth at the Wiggler
* dcm_theta: [deg] Rotation angle of the DCM. 0=set from energy E0
* sample_material: [str] reflection/structure data file for the single crystal sample
*
* %Link
* https://www.synchrotron-soleil.fr/en/beamlines/sixs
*
* %End
*******************************************************************************/
DEFINE INSTRUMENT SOLEIL_SIXS(E0=3, dE=0.1, dcm_theta=0, 
  string sample_material="CaCO3Ti", string sample_geometry="wire.ply")

DECLARE
%{
  double dcm_gap;
%}

INITIALIZE
%{
  double DM= 5.4909;     // Si d-spacing
  double d = DM/sqrt(3); // <111> reflection |<111>|=3
  dcm_gap  = 0.02;       // gap between the 2 monochromator crystals
   
  if (!dcm_theta && E0) {
    // n.lambda = 2 d sin(dcm_theta) = 2*PI/E2K / E0 with n=ORDER=1
    double sin_theta = 2*PI/E2K/E0 / 2 / d;
    if (fabs(sin_theta) < 1)
      dcm_theta = asin(sin_theta)*RAD2DEG;
  } else if (dcm_theta && !E0)
    E0 = 2*PI/E2K / (2*d*sin(dcm_theta*DEG2RAD));
  if (!dcm_theta || !E0 || dE <=0)
    exit(fprintf(stderr, "%s: ERROR: Monochromator can not reflect E0=%g +/- %g [keV]. Aborting.\n", NAME_INSTRUMENT, E0, dE));
  printf("%s: E0=%g [keV] Monochromator dcm_theta=%g [deg] \n", NAME_INSTRUMENT, E0, dcm_theta);
  if (fabs(dE/E0)>0.1) dE = 3e-2*E0;
%}

TRACE

COMPONENT origin = Progress_bar()
AT (0, 0, 0) RELATIVE ABSOLUTE

// the photon source -----------------------------------------------------------
COMPONENT Source_U20 = Undulator(
    E0 = E0,
    dE = dE,
    Ee = 2.75,
    Ie = 0.5,
    K = 5,
    sigex = 388e-6,
    sigey = 8.1e-6,
    sigepx = 14.5e-6,
    sigepy = 4.61e-6)
AT (0,0,0) RELATIVE origin

COMPONENT mon_src_xy = Monitor_nD(
  options="x y", xwidth=2e-3, yheight=2e-3, bins=128)
AT (0,0,13.3) RELATIVE source

COMPONENT mon_src_e = Monitor_nD(options="energy", xwidth=2e-3, yheight=2e-3, bins=128,
  min=E0-dE*1.1, max=E0+dE*1.1)
AT (0,0,0) RELATIVE PREVIOUS

COMPONENT slit = Slit(xwidth=0.5e-3, yheight=0.5e-3)
AT (0,0,0.1) RELATIVE PREVIOUS

COMPONENT slit_mon_xy = COPY(mon_src_xy)
AT (0,0,0) RELATIVE PREVIOUS

// The double monochromator ----------------------------------------------------
COMPONENT DCM_location = Arm()
AT (0,0,20.5) RELATIVE source

COMPONENT dcm_xtal0 = Bragg_crystal(
    length=0.04, width=0.04, 
    h=1, k=1, l=1, material="Si.txt", V=160.1826)
AT(0,0,0)          RELATIVE PREVIOUS
ROTATED (-dcm_theta,0,0) RELATIVE PREVIOUS
EXTEND %{
  if (!SCATTERED) ABSORB;
%}

COMPONENT dcm0      = Arm()
AT(0,0,0)          RELATIVE PREVIOUS
ROTATED (-dcm_theta,0,0) RELATIVE PREVIOUS

COMPONENT dcm_xtal1 = COPY(dcm_xtal0)
AT(0,dcm_gap, dcm_theta ? dcm_gap/tan(dcm_theta*DEG2RAD) : 0)    RELATIVE dcm_xtal0
ROTATED (dcm_theta,0,0)  RELATIVE PREVIOUS
EXTEND %{
  if (!SCATTERED) ABSORB;
%}

COMPONENT dcm1      = Arm()
AT(0,0,0)          RELATIVE PREVIOUS
ROTATED (dcm_theta,0,0)  RELATIVE PREVIOUS 

COMPONENT mon_dcm_e = COPY(mon_src_e)
AT (0,0,0.5) RELATIVE PREVIOUS

COMPONENT mon_dcm_xy = COPY(mon_src_xy)
AT (0,0,0) RELATIVE PREVIOUS

// The sample area -------------------------------------------------------------
COMPONENT sample_stage = COPY(mon_src_xy)
AT (0,0,8.13) RELATIVE PREVIOUS

COMPONENT fluo_rotated = Arm()
AT (0,0,0) RELATIVE PREVIOUS
ROTATED (0,30,0) RELATIVE PREVIOUS

COMPONENT sample = Fluorescence(
  xwidth=0.001, yheight=0.001, zdepth=0.5e-3,
  material=sample_material, geometry=sample_geometry, p_interact=0.8,
  target_index=1, focus_xw=0.1, focus_yh=0.1)
AT (0,0,0) RELATIVE sample_stage

COMPONENT mon_spl_fluo = Monitor_nD(options="energy", 
  xwidth=0.1, yheight=0.1, bins=1000,
  min=0, max=E0+dE*1.1)
AT (0,0,0.1) RELATIVE fluo_rotated
  

FINALLY
%{
%}

END
