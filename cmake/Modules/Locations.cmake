include_guard()

#common place to define locations
option( MCCODE_USE_LEGACY_DESTINATIONS "Whether or not to install files to legacy (pre Nov 2022) locations" ON )

if ( NOT MCCODE_VERSION OR NOT FLAVOR )
  message(FATAL_ERROR "Locations module should only be invoked after FLAVOR and MCCODE_VERSION have been set" )
endif()

function( configure_destination_dirs resultvarname )
  set( provided_vars "")
  macro( destset varname content )
    list(APPEND provided_vars "${varname}" "${content}")
  endmacro()

  #NOTE: Please update mccode_config.json.in if adding new variables here!

  if ( MCCODE_USE_LEGACY_DESTINATIONS )
    message( "Choosing backwards compatible installation destinations (disable via MCCODE_USE_LEGACY_DESTINATIONS=OFF)" )
    set( dest_prefix "${FLAVOR}/${MCCODE_VERSION}" )
    destset( DEST_BINDIR "${dest_prefix}/bin" )
    destset( DEST_LIBDIR "${dest_prefix}/libs" )
    destset( DEST_DATADIR_DATAFILES "${dest_prefix}/data" )
    destset( DEST_DATADIR_CODEFILES "${dest_prefix}/share" )
    destset( DEST_DATADIR_COMPS "${dest_prefix}" )
    destset( DEST_DATADIR_EXAMPLES "${dest_prefix}/examples" )
    destset( DEST_DATADIR_TOPENVFILES "${dest_prefix}" )
    destset( DEST_DATADIR_INFO "${dest_prefix}" )
    destset( DEST_DATADIR_EDITORS "${dest_prefix}/editors" )
    destset( DEST_DATADIR_DOC "${dest_prefix}/doc" )
    destset( DEST_TOOLDIR "${dest_prefix}/tools" )
  else()
    include( GNUInstallDirs )
    destset( DEST_BINDIR "${CMAKE_INSTALL_BINDIR}")
    destset( DEST_LIBDIR "${CMAKE_INSTALL_LIBDIR}")
    destset( DEST_DATADIR_DATAFILES "${CMAKE_INSTALL_DATADIR}/${FLAVOR}/data")
    destset( DEST_DATADIR_CODEFILES "${CMAKE_INSTALL_DATADIR}/${FLAVOR}/complib/share")
    destset( DEST_DATADIR_COMPS "${CMAKE_INSTALL_DATADIR}/${FLAVOR}/complib")
    destset( DEST_DATADIR_EXAMPLES "${CMAKE_INSTALL_DATADIR}/${FLAVOR}/examples")
    destset( DEST_DATADIR_TOPENVFILES "${CMAKE_INSTALL_DATADIR}/${FLAVOR}" )
    destset( DEST_DATADIR_INFO "${CMAKE_INSTALL_DATADIR}/${FLAVOR}/info" )
    destset( DEST_DATADIR_EDITORS "${CMAKE_INSTALL_DATADIR}/${FLAVOR}/editors" )
    destset( DEST_DATADIR_DOC "${CMAKE_INSTALL_DATADIR}/${FLAVOR}/doc" )
    destset( DEST_TOOLDIR "${CMAKE_INSTALL_DATADIR}/${FLAVOR}/tools" )
  endif()
  set(${resultvarname} "${provided_vars}" PARENT_SCOPE)
endfunction()

if ( MCCODE_USE_LEGACY_DESTINATIONS )
  set ( MCCODE_LEGACY_PATHS 1 )
else()
  set ( MCCODE_LEGACY_PATHS 0 )
endif()

function( apply_destination_dirs dir_list )
  while( dir_list )
    list( POP_FRONT dir_list varname varval)
    set(${varname} "${varval}" PARENT_SCOPE)
    message("McCode install destination: ${varname}=\"${varval}\"")
  endwhile()
endfunction()

configure_destination_dirs( tmp_detected_var_list )
apply_destination_dirs( "${tmp_detected_var_list}" )
unset( tmp_detected_var_list )
